buildscript {
    repositories {
        jcenter()
	mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.0'
    }
}

apply plugin: 'com.android.application'

repositories {
	mavenCentral()
}

dependencies {
        compile 'com.google.code.gson:gson:2.2.4'
        compile "com.android.support:appcompat-v7:20.0.0"
        compile 'com.android.support:support-v4:20.0.0'
    	compile 'com.android.support:support-v13:20.0.0'
        compile 'com.squareup.retrofit:retrofit:1.6.1'
        compile 'com.squareup.okhttp:okhttp:2.0.0'
        compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
        compile 'com.squareup:otto:1.3.4'
        compile 'com.jakewharton:disklrucache:2.0.2'
}

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

	lintOptions {
		lintConfig file("custom-lint.xml")
	}


    defaultConfig {
        applicationId "com.newfivefour.natcher"
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
    	androidTest.setRoot('src/test')
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
    	sourceCompatibility JavaVersion.VERSION_1_7
    	targetCompatibility JavaVersion.VERSION_1_7
    }


}

// Unit tests

sourceSets {
        unitTest {
                java.srcDir file('src/test/java')
                resources.srcDir file('src/test/resources')
        }
}

dependencies {
        unitTestCompile files("$project.buildDir/intermediates/classes/debug")
        unitTestCompile 'junit:junit:4.11'
        unitTestCompile 'org.robolectric:robolectric:2.1.1'
        unitTestCompile 'com.google.android:android:4.0.1.2'
	unitTestCompile 'org.hamcrest:hamcrest-all:1.3'
        unitTestCompile 'org.mockito:mockito-all:1.9.5'
        androidTestCompile files("$project.buildDir/classes/debug")
        androidTestCompile 'junit:junit:4.11'
        androidTestCompile 'org.robolectric:robolectric:2.1.1'
        androidTestCompile 'com.google.android:android:4.0.1.2'
	androidTestCompile 'org.hamcrest:hamcrest-all:1.3'
        androidTestCompile 'org.mockito:mockito-all:1.9.5'
}

configurations {
        unitTestCompile.extendsFrom runtime
        unitTestRuntime.extendsFrom unitTestCompile
}

task unitTest(type:Test, dependsOn: assemble) {
        description = "run unit tests"
        testClassesDir = project.sourceSets.unitTest.output.classesDir
        classpath = project.sourceSets.unitTest.runtimeClasspath
}

